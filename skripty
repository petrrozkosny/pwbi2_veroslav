dim_roky = 


ADDCOLUMNS(
ALL(dim_datumy[rok]),
"srazky rocni",
ROUND(
CALCULATE(
SUM(data[Srazky])),2))

____________________________

01 suma srazek v dane lokalite za vsechny roky = 
SUMX(
FILTER(
    ALL(data),
data[Lokalita] = SELECTEDVALUE(data[Lokalita])),
data[Srazky])

___________________________

02 suma srazek mesic 6 = 
-- v danem roce, lokalite
SUMX(
    FILTER(data,data[mesic] = 6),
data[Srazky])

_____________________________________

03 suma srazek = 
-- scenar1: Respektuji vsechny filtry, zadne nerusim,
-- nepridavam, tj. pouziji zakladni agregacni funkce
SUM(data[Srazky])

__________________________________

04 suma srazek mesic 6 = 
-- respektuji filtry vizualu, pridavam dalsi


-- je to pomalé, zbytečně moc dat
-- respektujeme pripadne filtry nad mesic
-- a pridavame dalsi
VAR varianta1 =
CALCULATE(
    SUM(data[Srazky]),
    FILTER(data,
    data[mesic] = 6))
-- Verze 2
-- TAKTO ANO, pokud nefunguje verze 3
-- Pozor, tady už se zbavuji případného filtru
-- nad sloupcem měsíc, tj. už to není scénář 2, ale
-- scénář 4
VAR varianta2 =
CALCULATE(
    SUM(data[Srazky]),
    FILTER(ALL(data[mesic]),
    data[mesic] = 6))
-- TAKTO ANO
-- verze 3
-- Pozor, tady už se zbavuji případného filtru
-- nad sloupcem měsíc, tj. už to není scénář 2, ale
-- scénář 4
VAR varianta3= 
CALCULATE(SUM(data[Srazky]),data[mesic]  = 6)

return varianta3

______________________________________________________________________

06 suma srazek ruzyne = 
CALCULATE(
    SUM(data[Srazky]),
    data[Lokalita] = "RUZYNE")

_______________________________________________________________________
07 podil srazek v roce = 
-- suma srazek v danem roce, mesici
SUM(data[Srazky])
-- deleno
/
-- suma srazek v danem roce, vsech mesicich
CALCULATE(
    SUM(data[Srazky]),
ALL(data[mesic]))

_______________________________________________________________-

08 podil srazek ruzyne = 

-- suma srazek v danem roce, mesici, ruzyne
CALCULATE(
    SUM(data[Srazky]),
data[Lokalita] = "RUZYNE")
-- deleno
/
-- suma srazek v danem roce, mesici, vsechny lokality

SUM(data[Srazky])

________________________________________________________________

09 srazky kumulativne ruzyne = 
-- kumulativni suma srazek v ruzyne, v danem roce

CALCULATE(
    SUM(data[Srazky]),
data[Lokalita] = "RUZYNE" &&
data[mesic] <= SELECTEDVALUE(data[mesic]))

_____________________________________________________________

11 suma srazek predchozi rok = 

CALCULATE(
    SUM(data[Srazky]),
data[rok] = SELECTEDVALUE(data[rok] )- 1)

10 suma srazek kumulativne = 
CALCULATE(
    SUM(data[Srazky]),
ALL(data[mesic],data[rok]),
data[Datum] <= MAX(data[Datum]))

___________________________________________________________

12 suma srazek mesic 1 ruzyne = 
-- v danem roce
CALCULATE(
SUM(data[Srazky]),
// FILTER(
// ALL(data[mesic],data[Lokalita]),data[mesic] = 1
// && data[Lokalita] = "RUZYNE")
data[Lokalita] = "RUZYNE" && data[mesic] = 1

)

______________________________________________________

13 suma srazek vsechny roky = 

-- suma srazek v dane lokalite, vsechny roky

CALCULATE(
    SUM(data[Srazky]),
    ALL(dim_datumy[rok]))

___________________________________________________-

cv02 podil srazek = 

-- suma v roce, zemi, lokalite
SUM(data[Srazky])
-- suma v roce, zemi
/
CALCULATE(
    SUM(data[Srazky]),
    ALL(dim_lokality[Lokalita]))

__________________________________________________________

cv03 nejdestivejsi lokalita kontrola = 
IF(
-- maximalni srazky dane zeme, lokality, roku

MAX(data[Srazky])
=
-- maximalni srazky v roce, zemi
CALCULATE(
    MAX(data[Srazky]),
ALL(dim_lokality[Lokalita])), "nejdestivejsi","ostatni")

_________________________________________________________________

dim_datumy = 
ADDCOLUMNS(
    SELECTCOLUMNS(
    CALENDARAUTO(),
    "datum",
    [Date]),
"rok",YEAR([datum]),
"kvartal",QUARTER([datum]),
"mesic",MONTH([datum]),
"konec_mesice",EOMONTH([datum],0))

_________________________________________________________________

16 suma srazek cely predchozi kvartal = 

VAR kvartal  = MIN(dim_datumy[kvartal])
VAR zacatek_kvartalu = 
CALCULATE(MIN(dim_datumy[datum]),ALL(dim_datumy[mesic]),
dim_datumy[kvartal] = kvartal)
VAR zacatek_predchoziho_kvartalu = EOMONTH(zacatek_kvartalu,-4)+1
VAR konec_predchoziho_kvatalu = EOMONTH(zacatek_predchoziho_kvartalu,3)

RETURN
CALCULATE(
    SUM(data[Srazky]),
    dim_datumy[datum]>=zacatek_predchoziho_kvartalu
    && dim_datumy[datum]<=konec_predchoziho_kvatalu,
    all(dim_datumy[rok],dim_datumy[mesic]))

________________________________________________________________

19 rok maximalnich srazek = 

VAR tabulka = 
ADDCOLUMNS(
    ALL(data[rok]),
"rocni srazky",
CALCULATE(
SUM(data[Srazky])))

VAR max_rocni_srazky = 
maxx(tabulka,[rocni srazky])

RETURN

CONCATENATEX(
    FILTER(tabulka,
    [rocni srazky] = max_rocni_srazky),
    data[rok],", ")



_____________________________________________20 podminene formatovani rocni srazky = 

-- tabulka se vsemi roky dane lokality, doplnena o sloupec s rocnimi srazkami

VAR tabulka = 
    ADDCOLUMNS(
        ALL(data[rok]),
    "rocni srazky",
    CALCULATE(
    sum(data[Srazky]))
    )

VAR max_tabulka =
MAXX(tabulka,[rocni srazky])

VAR min_tabulka = 
MINX(tabulka,[rocni srazky])

RETURN
SWITCH(TRUE(),
SUM(data[Srazky]) = max_tabulka,"CYAN",
SUM(data[Srazky]) = min_tabulka,"GOLD")

_______________________________________________________________


21 maximalni teplota obdobi = 

SWITCH(TRUE(),
SEARCH("pololeti",SELECTEDVALUE(obdobi[obdobi]),1,0) = 0,
CALCULATE(
    MAX(data[Teplota]),
    data[mesic] = CONVERT(SELECTEDVALUE(obdobi[obdobi]),INTEGER)),
SEARCH("1. pololeti",SELECTEDVALUE(obdobi[obdobi]),1,0)>0,
CALCULATE(
    MAX(data[Teplota]),
    data[mesic] <= 6),
SEARCH("2. pololeti",SELECTEDVALUE(obdobi[obdobi]),1,0)>0,
CALCULATE(
    MAX(data[Teplota]),
    data[mesic] > 6))

______________________________________

23 kumulativni suma srazek predchozi rok = 
CALCULATE(
    SUM(data[Srazky]),
dim_datumy[rok] = SELECTEDVALUE(dim_datumy[rok]) - 1
&& dim_datumy[mesic] <= SELECTEDVALUE(dim_datumy[mesic]))

